apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = "com.vc137.boids.Main"

dependencies {
    compile project(":common-jvm")
    compile dep.kotlin_stdlib
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.vc137.boids.MainKt',
                'Implementation-Version': version
    }
    baseName = 'boids'

    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    with jar
}

task runSimulation(dependsOn: classes, type:JavaExec) {
    main = 'com.vc137.boids.Main'
    classpath = sourceSets.main.runtimeClasspath
    ignoreExitValue(true)
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.kotlin
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin/'
    test.java.srcDirs += 'src/test/kotlin/'
}

artifacts {
    archives sourcesJar
}